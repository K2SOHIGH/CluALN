import pandas as pd
import os
import yaml
import sys
import glob
import multiprocessing

from scripts import snakeutils as su

"""
    dependencies :
        - mmseqs
    input [required] :
        - fasta file 
        - output directory path
    input [optional] :
        - old_fasta : previous fasta file used for clustering
        - old_seqdb : -
        - old_cludb : -
        - coverage : - 
        - clumode : - 
        - verbose : [0..3]
    output:
        - config["res_dir"]/cluDB.tsv
        - config["res_dir"]/cluDB
        - config["res_dir"]/seqDB
"""




def additional_targets(wildcards):
    MERGE_TARGET = [] 
    if config ["merge"]:    
        """
            each cluster will be aligned and pairwise identity will be compute AND msaS will be merged.
        """
        MERGE_TARGET = [
            os.path.join(config['res_dir'],'merged','merge.done'),
            os.path.join(config['res_dir'],'representatives','rep.done')
        ]
    return MERGE_TARGET

rule all:
    """
        Final rule
    """
    output:
        os.path.join(config['res_dir'],'summary.txt')
    input:  
        os.path.join(config['res_dir'],'clustering','clu.done'),
        os.path.join(config['res_dir'],'per_cluser','per_clu_msa.done'),
        additional_targets,
    params:
        resdir = config["res_dir"],
    script:
        os.path.join('scripts','summary.py')



# ------------------------------------------------------------------------------------------ #

# REPRESENTATIVES TARGET


rule rep_target:
    output:
        touch(temp(os.path.join(config['res_dir'],'representatives','rep.done')))
    input:
        os.path.join(config["res_dir"],"representatives","tables","identity_percent.tsv"),
        os.path.join(config["res_dir"],"representatives","tables","identity_distance.tsv"),
        os.path.join(config["res_dir"],"representatives","tables","similarity_percent.tsv"),
        os.path.join(config["res_dir"],"representatives","tables","similarity_distance.tsv"),

rule extract_rep_aln:
    output:
        os.path.join(config["res_dir"],"representatives","alignements","msa.aln"),    
    input:
        fasta = os.path.join(config["res_dir"],"merged","alignements","msa.aln"),
        clufile = os.path.join(config["res_dir"],"clustering","tables","clusters.tsv"),    
    conda:
        os.path.join("envs","biopy.yaml")
    script:
        os.path.join("scripts",'get_representatives.py')
        



# ------------------------------------------------------------------------------------------ #

# MERGE PER CLUSTER MSAs INTO GLOBAL MSA AND EXTRACT REPRESENTATIVES MSA 
rule merge_target:
    output:
        touch(temp(os.path.join(config['res_dir'],'merged','merge.done')))
    input:
        os.path.join(config["res_dir"],"merged","tables","identity_percent.tsv"),
        os.path.join(config["res_dir"],"merged","tables","identity_distance.tsv"),
        os.path.join(config["res_dir"],"merged","tables","similarity_percent.tsv"),
        os.path.join(config["res_dir"],"merged","tables","similarity_distance.tsv"),


# ------------------------------------------------------------------------------------------ #

# REP AND PER CLUSTER MSA TARGETS
## PER CLUSTER
def aggregate_cluster_id(wildcards):
    import glob
    checkpoint_output = checkpoints.cluster2fasta.get(**wildcards).output[0]
    clusters = [os.path.basename(i) for i in glob.glob(os.path.join(checkpoint_output, "cluster_*" ))]
    idt = expand(os.path.join(config["res_dir"],"per_cluster","{cluster}","tables","identity_percent.tsv"),
        cluster = clusters)
    sim = expand(os.path.join(config["res_dir"],"per_cluster","{cluster}","tables","similarity_percent.tsv"),
        cluster = clusters)
    su = expand(os.path.join(config["res_dir"],"per_cluster","{cluster}","tables","summary.tsv"),
        cluster = clusters)
    return idt + sim + su


rule per_cluster_target:
    output:
        touch(temp(os.path.join(config['res_dir'],'per_cluser','per_clu_msa.done')))
    input:        
        aggregate_cluster_id,

## REP
rule clustering_target:
    output:
        touch(temp(os.path.join(config['res_dir'],'clustering','clu.done')))
    input:
        os.path.join(config["res_dir"],"clustering","tables","clusters.tsv"),
        os.path.join(config["res_dir"],"clustering","tables","consensus_length.tsv"),
    
# ------------------------------------------------------------------------------------------ #

# PAIRWISE MATRICES


rule identity_matrix:
    output:
        os.path.join(config["res_dir"],"{target}","tables","identity_percent.tsv"),
        os.path.join(config["res_dir"],"{target}","tables","identity_distance.tsv"),
        os.path.join(config["res_dir"],"{target}","tables","similarity_percent.tsv"),
        os.path.join(config["res_dir"],"{target}","tables","similarity_distance.tsv"),
        os.path.join(config["res_dir"],"{target}","tables","summary.tsv"),
    input:
        msa = os.path.join(config["res_dir"],"{target}","alignements","msa.aln")
    log:
        os.path.join(config["res_dir"],"{target}","tables","msa_pairwise_comparison.log"),
    params:
        matrix = workflow.source_path("../resources/BLOSUM62.txt"),
        outdir = os.path.join(config["res_dir"],"{target}","tables"),
        distance = True,
    threads:
        config["threads"],
    conda:
        os.path.join("envs","biopy.yaml")
    script:
        os.path.join('scripts','msa_pairwise_comparison.py')

# ------------------------------------------------------------------------------------------ #

# MSA USING MAFFT 

rule cluster_msa2msa:
    output:
        os.path.join(config["res_dir"],"merged","alignements","msa.aln")
    input:
        msas = os.path.join(config["res_dir"],"merged",'input_msa'),
        tbl = os.path.join(config["res_dir"],"merged",'tbl_msa'),
    params:
        outdir = os.path.join(config["res_dir"],"merged","alignements"),
    conda:
        os.path.join("envs","mafft.yaml")
    threads:
        config["threads"] #multiprocessing.cpu_count()
    shell:
        'mkdir -p {params.outdir} ; '
        'if [[ $(cat {input.tbl} | grep -c "#") == 1 ]] ; then '
        '   cat {input.msas} > {output};'
        'else '
        '   mafft --thread {threads} --anysymbol --merge {input.tbl} {input.msas} > {output};'
        'fi'
        #--maxiterate 50


def aggregate_cluster(wildcards):
    import glob
    checkpoint_output = checkpoints.cluster2fasta.get(**wildcards).output[0]
    clusters = [os.path.basename(i) for i in glob.glob(os.path.join(checkpoint_output, "cluster_*" ))]
    return expand(os.path.join(config["res_dir"],"per_cluster","{cluster}","alignements","msa.aln"),
        cluster = clusters)


rule mafft_merge_input:
    output:
        msas = os.path.join(config["res_dir"],"merged",'input_msa'),
        tbl = os.path.join(config["res_dir"],"merged",'tbl_msa'),
    input:
        aggregate_cluster,
    script:
        os.path.join("scripts",'makemergeinput.py')


rule mafft:
    """
        where target = /per_cluser/<cluster_id>
    """
    output:
        os.path.join(config["res_dir"],"per_cluster", "{cluster}","alignements","msa.aln"), 
    input:
        os.path.join(config["res_dir"],"per_cluster","{cluster}","fastas","seq.fasta"),  
    conda:
        os.path.join("envs","mafft.yaml")
    threads:
        10 
    shell:
        "mafft --thread {threads} --anysymbol --quiet {input} > {output}"


checkpoint cluster2fasta:
    output:
        directory(os.path.join(config["res_dir"],"per_cluster")),
    input:
        clu = os.path.join(config["res_dir"],"clustering","tables","clusters.tsv"),
        fa = os.path.join(config["res_dir"],"clustering","fastas","input.fasta"),
    conda:
        os.path.join("envs","biopy.yaml")
    script:
        os.path.join("scripts",'clu2fasta.py')

# ------------------------------------------------------------------------------------------ #

# CLUSTERING

rule get_consensus_length:
    output:
        os.path.join(config["res_dir"],"clustering","tables","consensus_length.tsv")
    input:
        os.path.join(config["res_dir"],"clustering","fastas","consensus.fa"),
    conda:
        os.path.join("envs","biopy.yaml")
    script:
        os.path.join('scripts','get_length.py')


rule profiles2consensus:
    output:
        fasta = temp(os.path.join(config["res_dir"],"clustering","fastas","consensus.fa"))
    input:
        os.path.join(config["res_dir"],"clustering","profilDB","clu2profile.done")
    params:
        consensusDB = os.path.join(config["res_dir"],"clustering","profilDB","consensusDB"),
        proDB = os.path.join(config["res_dir"],"clustering","profilDB","proDB"),
        verbose = config["verbose"],
    conda:
        os.path.join("envs","mmseqs_13.45.yaml")
    shell:
        "mmseqs profile2consensus {params.proDB} {params.consensusDB} -v {params.verbose}  && "
        "mmseqs convert2fasta {params.consensusDB} {output.fasta} -v {params.verbose}"

rule cluster2profiles: 
    output:
        os.path.join(config["res_dir"],"clustering","profilDB","clu2profile.done"),
    input:
        os.path.join(config["res_dir"],"clustering","tables","clusters.tsv"),
    params:
        outdir = os.path.join(config["res_dir"],"clustering","profilDB"),
        seqDB = os.path.join(config["res_dir"],"clustering","clusterDB","seqDB"),        
        cluDB = os.path.join(config["res_dir"],"clustering","clusterDB","cluDB"),   
        verbose = config["verbose"],
    conda:
        os.path.join("envs","mmseqs_13.45.yaml")
    shell:
        "mkdir -p {params.outdir} && "
        "mmseqs result2profile {params.seqDB} {params.seqDB} {params.cluDB} {params.outdir}/proDB -v {params.verbose} && touch {output}  "


rule representative2fasta:
    output:
        os.path.join(config["res_dir"],"clustering","fastas","representative.fasta"),    
    input:
        os.path.join(config["res_dir"],"clustering","tables","clusters.tsv"),
    params:
        tmp = os.path.join(config["res_dir"],"clustering","clusterDB",'tmp'),
        seqDB = os.path.join(config["res_dir"],"clustering","clusterDB","seqDB"),        
        cluDB = os.path.join(config["res_dir"],"clustering","clusterDB","cluDB"),    
        verbose=config["verbose"],
    conda:
        os.path.join("envs","mmseqs_13.45.yaml")
    shell:
        "mmseqs createsubdb {params.cluDB} {params.seqDB} {params.cluDB}.rep ;"
        "mmseqs convert2fasta {params.cluDB}.rep {output}" 

rule cluster2tsv:
    output:
        os.path.join(config["res_dir"],"clustering","tables","clusters.tsv"),    
    input:
        os.path.join(config["res_dir"],"clustering","clustering.done"),
    params:
        tbldir = os.path.join(config["res_dir"],"clustering","tables"),
        tmp = os.path.join(config["res_dir"],"clustering","clusterDB",'tmp'),
        seqDB = os.path.join(config["res_dir"],"clustering","clusterDB","seqDB"),        
        cluDB = os.path.join(config["res_dir"],"clustering","clusterDB","cluDB"),    
        verbose=config["verbose"],
    conda:
        os.path.join("envs","mmseqs_13.45.yaml")
    shell:
        "mkdir -p {params.tbldir} && "
        "mmseqs createtsv {params.seqDB} {params.seqDB} {params.cluDB} "
        "{output} -v {params.verbose}"


rule clustering:
    output:
        os.path.join(config["res_dir"],"clustering","clustering.done"),
    input:
        os.path.join(config["res_dir"],"clustering","createdb.done")
    conda:
        os.path.join("envs","mmseqs_13.45.yaml")   
    params:
        seqDB = os.path.join(config["res_dir"],"clustering","clusterDB","seqDB"),
        cluDB = os.path.join(config["res_dir"],"clustering","clusterDB","cluDB"), #output
        tmp = os.path.join(config["res_dir"],"clustering","clusterDB",'tmp'),
        clumode = config["clumode"],
        coverage = config["coverage"],
        covmode = config["covmode"],
        pid = config["pid"],
        verbose=config["verbose"]
    shell:
        "mmseqs cluster {params.seqDB} {params.cluDB} {params.tmp} "
        "--cluster-mode {params.clumode} "
        "--min-seq-id {params.pid} "
        "--cov-mode {params.covmode} "
        "-c {params.coverage} "
        "-v {params.verbose}  && echo success > {output}"


rule create_db:
    output:
        os.path.join(config["res_dir"],"clustering","createdb.done"),
    input:
        os.path.join(config["res_dir"],"clustering","fastas","input.fasta")
    params:
        out = os.path.join(config["res_dir"],"clustering","clusterDB"),
        tmp = os.path.join(config["res_dir"],"clustering","clusterDB",'tmp'),
        verbose = config["verbose"],
    conda:
        os.path.join("envs","mmseqs_13.45.yaml")     
    shell:
        "mkdir -p {params.tmp} && "
        "mmseqs createdb {input[0]} {params.out}/seqDB -v {params.verbose} &&"
        "echo success > {output} 2> {output}"


rule gunzip_cds:
    output:
        os.path.join(config["res_dir"],"clustering","fastas","input.fasta"),
    input:
        config['clu_input'] ,
    shell:        
        'if [[ {input} == *.gz ]] ; then '
        'gunzip -c {input} ; '
        'else '
        'cat {input} ; '        
        'fi > {output} '
